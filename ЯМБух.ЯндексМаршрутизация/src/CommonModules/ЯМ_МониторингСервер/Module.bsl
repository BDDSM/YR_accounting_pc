
Функция ПолучитьДанныеМашинДляМониторинга(Машина = Неопределено) Экспорт 
	
	Настройки = ЯМ_РаботаСНастройками.ПолучитьНастройкиМодуля();
	
	МассивДанных = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЯМ_ТранспортныеСредства.Ссылка КАК Ссылка,
	|	"""" КАК company_id,
	|	ЯМ_ТранспортныеСредства.ЛогинКурьера КАК id,
	|	ЯМ_ТранспортныеСредства.Наименование КАК name,
	|	"""" КАК number,
	|	ЯМ_ТранспортныеСредства.НомерТелефона КАК phone,
	|	ЛОЖЬ КАК sms_enabled
	|ИЗ
	|	Справочник.ЯМ_ТранспортныеСредства КАК ЯМ_ТранспортныеСредства
	|ГДЕ
	|	ЯМ_ТранспортныеСредства.Работает
	|	И НЕ ЯМ_ТранспортныеСредства.ПометкаУдаления
	|	И ЯМ_ТранспортныеСредства.Ссылка = &Машина";
	Если Машина = Неопределено Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ЯМ_ТранспортныеСредства.Ссылка = &Машина", "");
	Иначе
		Запрос.УстановитьПараметр("Машина", Машина);
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		//СтруктураДанных = Новый Структура("company_id,id,name,number,phone,sms_enabled");
		СтруктураДанных = Новый Структура("company_id,name,number,phone,sms_enabled");
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);
		СтруктураДанных.number = Выборка.id;
		СтруктураДанных.company_id = Настройки.idКомпании;
		//СтруктураДанных.number = Строка(Выборка.Ссылка.УникальныйИдентификатор());
		МассивДанных.Добавить(СтруктураДанных);
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

Функция ПолучитьДанныеСкладовДляМониторинга(Склад = Неопределено) Экспорт 
	
	МассивДанных = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЯМ_СкладыОтгрузки.Ссылка КАК Ссылка,
	|	ЯМ_СкладыОтгрузки.Адрес КАК address,
	|	ЛОЖЬ КАК allow_route_editing,
	|	ЯМ_СкладыОтгрузки.Код КАК id,
	|	ЯМ_СкладыОтгрузки.Широта КАК lat,
	|	ЯМ_СкладыОтгрузки.Долгота КАК lon,
	|	100 КАК mark_route_started_radius,
	|	ЯМ_СкладыОтгрузки.Наименование КАК name,
	|	"""" КАК number,
	|	"""" КАК order_service_duration_s,
	|	"""" КАК service_duration_s,
	|	ЯМ_СкладыОтгрузки.ВременноеОкно КАК time_interval,
	|	""Europe/Moscow"" КАК time_zone
	|ИЗ
	|	Справочник.ЯМ_СкладыОтгрузки КАК ЯМ_СкладыОтгрузки
	|ГДЕ
	|	ЯМ_СкладыОтгрузки.Работает
	|	И НЕ ЯМ_СкладыОтгрузки.ПометкаУдаления
	|	И ЯМ_СкладыОтгрузки.Ссылка = &Склад";
	Если Склад = Неопределено Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ЯМ_СкладыОтгрузки.Ссылка = &Склад", "");
	Иначе
		Запрос.УстановитьПараметр("Склад", Склад);
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		// СтруктураДанных = Новый Структура("address,allow_route_editing,id,lat,lon,mark_route_started_radius,name,number,order_service_duration_s,service_duration_s,time_interval,time_zone");
		СтруктураДанных = Новый Структура("address,allow_route_editing,id,lat,lon,mark_route_started_radius,name,number,time_interval,time_zone");
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);
		СтруктураДанных.number = Строка(Выборка.id);
		//СтруктураДанных.number = Строка(Выборка.Ссылка.УникальныйИдентификатор());
		МассивДанных.Добавить(СтруктураДанных);
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

Функция ПодготовитьJsonСтроку(Данные) Экспорт
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	СтрокаJson = СтрЗаменить(ЗаписьJSON.Закрыть(),"\\","\");
	
	Возврат СтрокаJson;
КонецФункции

Функция ЗапросМониторинг(Метод,МассивДанных,Настройки) Экспорт
	Соединение = Новый HTTPСоединение("courier.yandex.ru",,,
			,Новый ИнтернетПрокси(Истина)
			,
			,Новый ЗащищенноеСоединениеOpenSSL);	
				
	// параметры подключения
	Адрес = "/api/v1/companies/" + Настройки.idКомпании + "/" + Метод;

	Запрос = Новый HTTPЗапрос(Адрес);
	Запрос.Заголовки.Вставить("Content-Type",	"application/json");
	Запрос.Заголовки.Вставить("Accept",			"application/json");
	Запрос.Заголовки.Вставить("Authorization",	"Auth " + Настройки.Токен_ЯндексМониторинг);
	
	// формирование запроса
	ТекстТелаЗапроса = ПодготовитьJsonСтроку(МассивДанных);
	Запрос.УстановитьТелоИзСтроки(ТекстТелаЗапроса);

	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	
	СтруктураОтвета = Новый Структура;
	СтруктураОтвета.Вставить("КодСостояния",Ответ.КодСостояния);
	
	Возврат СтруктураОтвета;
КонецФункции	